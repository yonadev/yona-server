plugins {
	id "application"
}

description = "AppService"

application {
	mainClass = "nu.yona.server.AppServiceApplication"
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

def dockerImageName = "${project.group}/${base.archivesName}"
def classifier = "full"

configurations {
	providedRuntime
	intTestImplementation.extendsFrom testImplementation
	intTestRuntime.extendsFrom testRuntime
}

dependencies {
	implementation project(":core")
	implementation libs.bcprov
	implementation libs.bcpkix
	runtimeOnly libs.mariadb.client
	runtimeOnly libs.jackson.datatype.jsr310
	runtimeOnly libs.jackson.datatype.jdk8

	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"

	testImplementation project(path: ":core", configuration: "testUtils")
	testImplementation libs.httpmime
	testImplementation libs.test.mockito.core

	intTestImplementation libs.test.groovy
	intTestImplementation libs.test.spock.core
	intTestImplementation libs.commons.lang3
}

// ---- Test Configuration ----
test {
	enableAssertions = true
}

// ---- Integration Test Setup ----
sourceSets {
	intTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

tasks.register("intTest", Test) {
	description = "Run integration tests (located in src/intTest/...)."
	group = "verification"
	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	useJUnitPlatform()

	systemProperties = System.getProperties() + [
		"java.util.logging.config.file": "../core/src/testUtils/logging.properties",
		"yona.adminservice.url"			  : project.ext.yona_adminservice_url,
		"yona.analysisservice.url"		  : project.ext.yona_analysisservice_url,
		"yona.appservice.url"			  : project.ext.yona_appservice_url,
		"yona.batchservice.url"			  : project.ext.yona_batchservice_url,
		"yona.enableHibernateStatsAllowed": project.ext.yona_enableHibernateStatsAllowed,
		"yona.testServer"				  : project.ext.yona_testServer
	]
}

// ---- Boot Run Configuration ----
bootRun {
	enableAssertions = true

	if (project.hasProperty("appservice.debug") || project.hasProperty("debug.all")) {
		jvmArgs = [
			"-Xdebug",
			"-Xrunjdwp:transport=dt_socket,address=${project.ext.yona_appservice_debug_port},server=y,suspend=n"
		]
	}

	systemProperties = [
		"server.port"	 : System.getProperty("server.port", project.ext.yona_appservice_port),
		"management.port": System.getProperty("management.port", project.ext.yona_appservice_mgmt_port)
	]
}

// ---- Jar Packaging ----
bootJar {
	archiveBaseName = "appservice"
	archiveClassifier.set(classifier)
}

// ---- Docker Configuration ----
docker {
	registryCredentials {
		username = project.findProperty("dockerHubUserName")
		password = project.findProperty("dockerHubPassword")
	}
	url = project.findProperty("dockerUrl") ?: "http://localhost:2375"
}

tasks.register("buildDockerImage", DockerBuildImage) {
	dependsOn bootJar
	def dockerBuildDir = file("$buildDir/docker")
	inputDir = dockerBuildDir
	images = ["${dockerImageName}:${project.ext.docker_tag}"]

	doFirst {
		dockerBuildDir.mkdirs()

		copy {
			from bootJar
			into dockerBuildDir
			rename("${jar.archiveBaseName.get()}-${jar.archiveVersion.get()}-${classifier}", jar.archiveBaseName.get())
		}

		copy {
			from "src/main/docker/Dockerfile"
			into dockerBuildDir
		}
	}
}

tasks.register("pushDockerImage", DockerPushImage) {
	dependsOn tasks.named("buildDockerImage")
	images = ["${dockerImageName}:${project.ext.docker_tag}"]
}
