description = "AppService"

apply plugin: "application"

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

jar {
	baseName = "appservice"
}

release {
	failOnCommitNeeded = true
	tagTemplate = "appservice-${version}"
	git {
		requireBranch = "yd-40-changes-for-building|master"
	}
}

configurations {
	providedRuntime
}

dependencies {
	compile project(":core")
	compile "org.bouncycastle:bcprov-jdk15on:1.56"
	compile "org.bouncycastle:bcpkix-jdk15on:1.56"
	runtime "org.mariadb.jdbc:mariadb-java-client:$project.ext.version_mariadb_client"
	runtime "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$project.ext.version_jackson"
	runtime "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$project.ext.version_jackson"
	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"

	testCompile project(path: ":core", configuration: "testUtils")
}

test {
	enableAssertions = true
}

bootRun {
	enableAssertions = true

	if ( project.hasProperty("appservice.debug") || project.hasProperty("debug.all")) {
		jvmArgs "-Xdebug", "-Xrunjdwp:transport=dt_socket,address=" + project.ext.yona_appservice_debug_port + ",server=y,suspend=n"
	}

	systemProperties = [
		"server.port": System.getProperty("server.port", project.ext.yona_appservice_port),
		"management.port": System.getProperty("management.port", project.ext.yona_appservice_mgmt_port)
	]
}

docker {
	registryCredentials {
		username = project.hasProperty("dockerHubUserName")? project.dockerHubUserName : null
		password = project.hasProperty("dockerHubPassword")? project.dockerHubPassword : null
	}
	url = project.hasProperty("dockerUrl")? project.dockerUrl : "http://localhost:2375"
}

task buildDockerImage(type: DockerBuildImage, dependsOn: bootRepackage) {
	inputDir = mkdir("build/docker")
	tag = "${project.group}/${jar.baseName}:${project.ext.docker_tag}"
	doFirst {
		copy {
			from bootRepackage
			into inputDir
			rename "${jar.baseName}-${jar.version}-${bootRepackage.classifier}", "${jar.baseName}"
		}
		copy {
			from "src/main/docker/Dockerfile"
			into "$inputDir"
		}
	}
}

task pushDockerImage(type: DockerPushImage, dependsOn: buildDockerImage) {
	imageName = "${project.group}/${jar.baseName}"
	tag = project.ext.docker_tag
}

sourceSets {
	intTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

configurations {
	intTestCompile.extendsFrom testCompile
	intTestRuntime.extendsFrom testRuntime
}

task intTest(type:Test){
	description = "Run integration tests (located in src/intTest/...)."
	testClassesDir = project.sourceSets.intTest.output.classesDir
	classpath = project.sourceSets.intTest.runtimeClasspath

	systemProperties = System.getProperties()

	systemProperties = [
		"yona.adminservice.url": project.ext.yona_adminservice_url,
		"yona.analysisservice.url": project.ext.yona_analysisservice_url,
		"yona.appservice.url": project.ext.yona_appservice_url,
		"yona.batchservice.url": project.ext.yona_batchservice_url,
		"yona.test.gen.data": project.ext.yona_test_gen_data
	]
}

bootRepackage {
	classifier = "full"
}
