plugins {
    id 'java-library'
}

description = 'Core'

jar {
	baseName = 'Core'
}

configurations {
	testUtils.extendsFrom testRuntime
}

sourceSets {
	testUtils
}

task testUtilsJar(type: Jar) {
	classifier "testUtils"
	from sourceSets.testUtils.output
}

artifacts {
	testUtils testUtilsJar
}

dependencies {
	// When updating the below two version numbers, also update
	// hazelcast.ReplicationController.image_version in k8s/helm/yona/values.yaml
	implementation "com.hazelcast:hazelcast:4.2"
	implementation "com.hazelcast:hazelcast-spring:4.2"
	implementation 'com.hazelcast:hazelcast-kubernetes:2.2.2'
	api "org.springframework.boot:spring-boot-starter-hateoas"
	api "org.springframework.boot:spring-boot-starter-data-jpa"
	api "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.cloud:spring-cloud-starter-sleuth:3.0.3"
	implementation "org.springframework.metrics:spring-metrics:0.5.1.RELEASE"
	implementation "io.micrometer:micrometer-registry-prometheus:1.7.0"
	api "org.springframework:spring-context-support"
	implementation "org.hibernate:hibernate-java8:5.4.31.Final"
	implementation "org.atteo:evo-inflector:1.2.2"
	implementation "commons-validator:commons-validator:1.7"
	implementation "commons-lang:commons-lang:2.6"
	api "javax.mail:mail:1.4.7"
	api "org.thymeleaf:thymeleaf-spring5:3.0.12.RELEASE"
	implementation "org.springframework.ldap:spring-ldap-core:2.3.4.RELEASE"
	implementation "org.springframework.security:spring-security-config:5.5.0"
	implementation "org.springframework.security:spring-security-web:5.5.0"
	api "com.google.guava:guava:23.0"
	implementation "com.googlecode.libphonenumber:libphonenumber:8.12.24"
	implementation "com.google.firebase:firebase-admin:7.3.0"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.mockito:mockito-junit-jupiter"
	testImplementation "org.jmockit:jmockit:$project.ext.version_jmockit"
	testImplementation "org.junit.jupiter:junit-jupiter:$project.ext.version_junit_jupiter"
	testImplementation "com.spencerwi:hamcrest-jdk8-time:$project.ext.version_hamcrest_jdk8_time"
	testImplementation "nl.jqno.equalsverifier:equalsverifier:$project.ext.version_equalsverifier"

	testUtilsImplementation "org.codehaus.groovy:groovy-all:3.0.8"
	testUtilsImplementation "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"

	// mandatory dependencies for using Spock
	testUtilsImplementation "org.spockframework:spock-core:$project.ext.version_spock_core"
}

test {
	enableAssertions = true
	jvmArgs "-javaagent:${classpath.find { it.name.contains('jmockit') }.absolutePath}"
	useJUnitPlatform()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

eclipse {
	classpath {
		plusConfigurations += [configurations.testUtilsImplementation]
	}
}
