plugins {
	id 'java-library'
}

description = 'Core'

jar {
	archiveBaseName = 'Core'
}

configurations {
	testUtils.extendsFrom testRuntime
}

sourceSets {
	testUtils
}

task testUtilsJar(type: Jar) {
	archiveClassifier = "testUtils"
	from sourceSets.testUtils.output
}

artifacts {
	testUtils testUtilsJar
}

dependencies {
	implementation libs.hazelcast
	implementation libs.hazelcast.spring
	implementation libs.hazelcast.kubernetes
	api libs.spring.boot.starter.hateoas
	api libs.spring.boot.starter.data.jpa
	api libs.spring.boot.starter.web
	implementation libs.spring.boot.starter.actuator
	implementation libs.spring.cloud.starter.sleuth
	implementation libs.spring.metrics
	implementation libs.micrometer.prometheus
	api libs.spring.context.support
	implementation libs.hibernate
	implementation libs.evo.inflector
	implementation libs.commons.validator
	implementation libs.commons.lang
	api libs.jakarta.mail
	api libs.thymeleaf
	implementation libs.spring.ldap.core
	implementation libs.spring.security.web
	implementation libs.spring.security.config
	api libs.guava
	implementation libs.libphonenumber
	implementation libs.firebase.admin

	testImplementation libs.test.spring.boot.starter.test
	testImplementation libs.test.mockito.junit.jupiter
	testImplementation libs.test.jmockit
	testImplementation libs.test.junit.jupiter
	testImplementation libs.test.hamcrest.jdk8.time
	testImplementation libs.test.equalsverifier

	testUtilsImplementation libs.test.groovy
	testUtilsImplementation libs.httpmime

	// mandatory dependencies for using Spock
	testUtilsImplementation libs.test.spock.core
}

test {
	enableAssertions = true
	jvmArgs "-javaagent:${classpath.find { it.name.contains('jmockit') }.absolutePath}"
	useJUnitPlatform()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

eclipse {
	classpath {
		plusConfigurations += [configurations.testUtilsImplementation]
	}
}
