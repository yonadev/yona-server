description = "Batch service"

apply plugin: "application"

mainClassName = "nu.yona.server.BatchServiceApplication"


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

def dockerImageName = "${project.group}/${jar.baseName}"

release {
	failOnCommitNeeded = true
	tagTemplate = "batchservice-${version}"
	git {
		requireBranch = "yd-40-changes-for-building|master"
	}
}

configurations {
	providedRuntime
}

dependencies {
	compile project(":core")
	runtime "org.mariadb.jdbc:mariadb-java-client:$project.ext.version_mariadb_client"
	compile "org.springframework.batch:spring-batch-core:$project.ext.version_batch_core"
	compile "org.quartz-scheduler:quartz:2.3.2"
	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
}

test {
	enableAssertions = true
}

bootRun {
	enableAssertions = true

	if (project.hasProperty("batchservice.debug") || project.hasProperty("debug.all"))
	{
		jvmArgs "-Xdebug", "-Xrunjdwp:transport=dt_socket,address=" + project.ext.yona_batchservice_debug_port + ",server=y,suspend=n"
	}

	systemProperties = ["server.port"    : System.getProperty("server.port", project.ext.yona_batchservice_port),
						"management.port": System.getProperty("management.port", project.ext.yona_batchservice_mgmt_port)]
}

docker {
	registryCredentials {
		username = project.hasProperty("dockerHubUserName") ? project.dockerHubUserName : null
		password = project.hasProperty("dockerHubPassword") ? project.dockerHubPassword : null
	}
	url = project.hasProperty("dockerUrl") ? project.dockerUrl : "http://localhost:2375"
}

task buildDockerImage(type: DockerBuildImage, dependsOn: bootJar) {
	inputDir = mkdir("build/docker")
	images = ["${dockerImageName}:${project.ext.docker_tag}"]
	doFirst {
		copy {
			from bootJar
			into inputDir
			rename "${jar.baseName}-${jar.version}-${bootJar.classifier}", "${jar.baseName}"
		}
		copy {
			from "src/main/docker/Dockerfile"
			into inputDir
		}
	}
}

task pushDockerImage(type: DockerPushImage, dependsOn: buildDockerImage) {
	images = ["${dockerImageName}:${project.ext.docker_tag}"]
}

sourceSets {
	intTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

configurations {
	intTestCompile.extendsFrom testCompile
	intTestRuntime.extendsFrom testRuntime
}

task intTest(type: Test) {
	description = "Run integration tests (located in src/intTest/...)."
	testClassesDirs = project.sourceSets.intTest.output.classesDirs
	classpath = project.sourceSets.intTest.runtimeClasspath
}

bootJar {
	classifier = "full"
	baseName = "batchservice"
}
