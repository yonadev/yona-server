plugins {
	id "application"
}

description = "Batch service"

application {
	mainClass = "nu.yona.server.BatchServiceApplication"
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

def dockerImageName = "${project.group}/${base.archivesName.get()}"
def classifier = "full"

configurations {
	providedRuntime
	intTestImplementation.extendsFrom testImplementation
	intTestRuntime.extendsFrom testRuntime
}

dependencies {
	implementation project(":core")

	implementation libs.spring.batch.core
	implementation libs.quartz
	runtimeOnly libs.mariadb.client
	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
}

// ---- Test Configuration ----
test {
	enableAssertions = true
}

// ---- Integration Test Setup ----
sourceSets {
	intTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

tasks.register("intTest", Test) {
	description = "Run integration tests (located in src/intTest/...)."
	group = "verification"
	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	useJUnitPlatform()
}

// ---- Boot Run Configuration ----
bootRun {
	enableAssertions = true

	if (project.hasProperty("batchservice.debug") || project.hasProperty("debug.all")) {
		jvmArgs = [
			"-Xdebug",
			"-Xrunjdwp:transport=dt_socket,address=${project.ext.yona_batchservice_debug_port},server=y,suspend=n"
		]
	}

	systemProperties = [
		"server.port"	 : System.getProperty("server.port", project.ext.yona_batchservice_port),
		"management.port": System.getProperty("management.port", project.ext.yona_batchservice_mgmt_port)
	]
}

// ---- Jar Packaging ----
bootJar {
	archiveBaseName.set("batchservice")
	archiveClassifier.set(classifier)
}

// ---- Docker Configuration ----
docker {
	registryCredentials {
		username = project.findProperty("dockerHubUserName")
		password = project.findProperty("dockerHubPassword")
	}
	url = project.findProperty("dockerUrl") ?: "http://localhost:2375"
}

tasks.register("buildDockerImage", DockerBuildImage) {
	dependsOn bootJar
	def dockerBuildDir = file("$buildDir/docker")
	inputDir = mkdir(dockerBuildDir)
	images = ["${dockerImageName}:${project.ext.docker_tag}"]

	doFirst {
		copy {
			from bootJar
			into dockerBuildDir
			rename("${jar.archiveBaseName.get()}-${jar.archiveVersion.get()}-${classifier}", jar.archiveBaseName.get())
		}

		copy {
			from "src/main/docker/Dockerfile"
			into dockerBuildDir
		}
	}
}

tasks.register("pushDockerImage", DockerPushImage) {
	dependsOn tasks.named("buildDockerImage")
	images = ["${dockerImageName}:${project.ext.docker_tag}"]
}
