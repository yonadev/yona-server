/** Parses a version from a dependency declaration string */
static String versionOf(String dependencySpec) {
	return dependencySpec.split(':').last()
}

final Map<String, String> libraries = [
	// Dependabot will parse these dependencies.
	// Keep all of these as uninterpolated string literals so that Dependabot can parse the versions and create PRs for
	// upgrades.
	//
	// DO NOT interpolate version variables here because Dependabot is not smart enough to understand those. Dependabot's
	// version parsing is simply regex matching and never actually evaluates a gradle script.
	springBatchCore 		: "org.springframework.batch:spring-batch-core:4.3.3",
	springSecurityWeb		: "org.springframework.security:spring-security-web:5.5.1",
	mariadbClient			: "org.mariadb.jdbc:mariadb-java-client:2.7.4",
	jmockit					: "org.jmockit:jmockit:1.49",
	junitJupiter			: "org.junit.jupiter:junit-jupiter:5.7.2",
	hamcrestJdk8Time		: "com.spencerwi:hamcrest-jdk8-time:0.7.1",
	equalsverifier			: "nl.jqno.equalsverifier:equalsverifier:3.7",
	spockCore				: "org.spockframework:spock-core:2.0-groovy-3.0",
	bcprov					: "org.bouncycastle:bcprov-jdk15on:1.69",
	// When updating the below version number, also update
	// hazelcast.ReplicationController.image_version in k8s/helm/yona/values.yaml
	hazelcast				: "com.hazelcast:hazelcast:4.2.2",
]

// Export versions that are needed outside of this file (and elsewhere within)
final Map<String, String> v = [
	springSecurityWeb		: versionOf(libraries.springSecurityWeb),
	bcprov					: versionOf(libraries.bcprov),
	hazelcast				: versionOf(libraries.hazelcast),
]

// While Dependabot won't be able to parse these deps, these will get upgraded for free anyway since they share versions
// with dependencies declared above that are parseable by Dependabot. This is just a workaround to be DRY while still
// benefiting from Dependabot's automatic PR upgrades.
final Map<String, String> related = [
	springSecurityConfig	: "org.springframework.security:spring-security-config:${v.springSecurityWeb}",
	bcpkix					: "org.bouncycastle:bcpkix-jdk15on:${v.bcprov}",
	hazelcastSpring			: "com.hazelcast:hazelcast-spring:${v.hazelcast}",
]

ext {
	deps = libraries + related
	versions = v
}
