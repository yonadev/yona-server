plugins {
	id "java"
	id "eclipse"
	id "groovy"
	alias(libs.plugins.spring.dependency.mgmt)
	alias(libs.plugins.spring.boot).apply(false)
	alias(libs.plugins.bmuschko.docker).apply(false)
	id "project-report"
	alias(libs.plugins.sonarqube)
}

description = "Yona server"

allprojects {
	group = "yonadev"
}

htmlDependencyReport {
	projects = project.allprojects
}

subprojects {
	sourceCompatibility = 21.0
	targetCompatibility = 21.0
	apply plugin: "eclipse"
	apply plugin: "groovy"
	apply plugin: libs.plugins.spring.dependency.mgmt.get().pluginId
	apply plugin: libs.plugins.spring.boot.get().pluginId
	apply plugin: libs.plugins.bmuschko.docker.get().pluginId

	ext["hibernate.version"] = libs.versions.hibernate.get()
	ext["thymeleaf.version"] = libs.thymeleaf.get().versionConstraint

	project.ext {
		build_id = System.getenv().BUILD_ID ?: "local"
		docker_tag = "build-${build_id}"

		runList = project.properties["runList"] ?: []
		yona_adminservice_scheme = project.properties["yona_adminservice_scheme"] ?: "http"
		yona_adminservice_host = project.properties["yona_adminservice_host"] ?: "localhost"
		yona_adminservice_port = project.properties["yona_adminservice_port"] ?: "8180"
		yona_adminservice_mgmt_port = project.properties["yona_adminservice_mgmt_port"] ?: "9080"
		yona_adminservice_debug_port = project.properties["yona_adminservice_debug_port"] ?: "8840"
		yona_adminservice_url = project.properties["yona_adminservice_url"] ?: "${yona_adminservice_scheme}://${yona_adminservice_host}:${yona_adminservice_port}"
		yona_analysisservice_scheme = project.properties["yona_analysisservice_scheme"] ?: "http"
		yona_analysisservice_host = project.properties["yona_analysisservice_host"] ?: "localhost"
		yona_analysisservice_port = project.properties["yona_analysisservice_port"] ?: "8181"
		yona_analysisservice_mgmt_port = project.properties["yona_analysisservice_mgmt_port"] ?: "9081"
		yona_analysisservice_debug_port = project.properties["yona_analysisservice_debug_port"] ?: "8841"
		yona_analysisservice_url = project.properties["yona_analysisservice_url"] ?: "${yona_analysisservice_scheme}://${yona_analysisservice_host}:${yona_analysisservice_port}"
		yona_appservice_scheme = project.properties["yona_appservice_scheme"] ?: "http"
		yona_appservice_host = project.properties["yona_appservice_host"] ?: "localhost"
		yona_appservice_port = project.properties["yona_appservice_port"] ?: "8182"
		yona_appservice_mgmt_port = project.properties["yona_appservice_mgmt_port"] ?: "9082"
		yona_appservice_debug_port = project.properties["yona_appservice_debug_port"] ?: "8842"
		yona_appservice_url = project.properties["yona_appservice_url"] ?: "${yona_appservice_scheme}://${yona_appservice_host}:${yona_appservice_port}"
		yona_batchservice_scheme = project.properties["yona_batchservice_scheme"] ?: "http"
		yona_batchservice_host = project.properties["yona_batchservice_host"] ?: "localhost"
		yona_batchservice_port = project.properties["yona_batchservice_port"] ?: "8183"
		yona_batchservice_mgmt_port = project.properties["yona_batchservice_mgmt_port"] ?: "9083"
		yona_batchservice_debug_port = project.properties["yona_batchservice_debug_port"] ?: "8843"
		yona_batchservice_url = project.properties["yona_batchservice_url"] ?: "${yona_batchservice_scheme}://${yona_batchservice_host}:${yona_batchservice_port}"
		yona_enableHibernateStatsAllowed = project.properties["yona_enableHibernateStatsAllowed"] ?: "false"
		yona_testServer = project.properties["yona_testServer"] ?: "false"
	}

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
	}

	springBoot {
		buildInfo {
			properties {
				time = null
				additional = [buildId: project.ext.build_id]
			}
		}
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:all,-try'
		options.deprecation = true
	}

	dependencies {
		constraints {
			implementation("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.17, 3[")
					prefer("2.17.0")
				}
				because("CVE-2021-44228: Log4j vulnerable to remote code execution")
			}
		}
	}
}

eclipse {
	classpath {
		containers.remove("org.eclipse.jdt.launching.JRE_CONTAINER")
		containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8"
	}
}

wrapper {
	gradleVersion = "6.1"
}

sonarqube {
	properties {
		property "sonar.projectKey", "yonadev_yona-server"
		property "sonar.organization", "yonadev"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
