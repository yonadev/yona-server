description = "Analysis"

apply plugin: "application"

mainClassName = "nu.yona.server.AnalysisServiceApplication"


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

def dockerImageName = "${project.group}/${jar.baseName}"

release {
	failOnCommitNeeded = true
	tagTemplate = "analysisservice-${version}"
	git {
		requireBranch = "yd-40-changes-for-building|master"
	}
}

configurations {
	providedRuntime
	intTestImplementation.extendsFrom testImplementation
	intTestRuntime.extendsFrom testRuntime
}

dependencies {
	implementation project(":core")
	runtimeClasspath "org.mariadb.jdbc:mariadb-java-client:$project.ext.version_mariadb_client"
	runtimeClasspath "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
	runtimeClasspath "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"

	testImplementation project(path: ":core", configuration: "testUtils")
	testImplementation project(':core').sourceSets.test.output
	testImplementation "org.mockito:mockito-core"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.mockito:mockito-junit-jupiter"
	testImplementation "org.jmockit:jmockit:$project.ext.version_jmockit"

	intTestImplementation "org.codehaus.groovy:groovy-all:3.0.8"
	intTestImplementation "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
	intTestImplementation "org.spockframework:spock-core:$project.ext.version_spock_core"
}

test {
	enableAssertions = true
	jvmArgs "-javaagent:${classpath.find { it.name.contains('jmockit') }.absolutePath}"
	useJUnitPlatform()
}

bootRun {
	enableAssertions = true

	if (project.hasProperty("analysisservice.debug") || project.hasProperty("debug.all"))
	{
		jvmArgs "-Xdebug", "-Xrunjdwp:transport=dt_socket,address=" + project.ext.yona_analysisservice_debug_port + ",server=y,suspend=n"
	}

	systemProperties = ["server.port"    : System.getProperty("server.port", project.ext.yona_analysisservice_port),
						"management.port": System.getProperty("management.port", project.ext.yona_analysisservice_mgmt_port)]
}

docker {
	registryCredentials {
		username = project.hasProperty("dockerHubUserName") ? project.dockerHubUserName : null
		password = project.hasProperty("dockerHubPassword") ? project.dockerHubPassword : null
	}
	url = project.hasProperty("dockerUrl") ? project.dockerUrl : "http://localhost:2375"
}

task buildDockerImage(type: DockerBuildImage, dependsOn: bootJar) {
	inputDir = mkdir("build/docker")
	images = ["${dockerImageName}:${project.ext.docker_tag}"]
	doFirst {
		copy {
			from bootJar
			into inputDir
			rename "${jar.baseName}-${jar.version}-${bootJar.classifier}", "${jar.baseName}"
		}
		copy {
			from "src/main/docker/Dockerfile"
			into inputDir
		}
	}
}

task pushDockerImage(type: DockerPushImage, dependsOn: buildDockerImage) {
	images = ["${dockerImageName}:${project.ext.docker_tag}"]
}

sourceSets {
	intTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

task intTest(type: Test) {
	description = "Run integration tests (located in src/intTest/...)."
	testClassesDirs = project.sourceSets.intTest.output.classesDirs
	classpath = project.sourceSets.intTest.runtimeClasspath

	systemProperties = ["java.util.logging.config.file": "../core/src/testUtils/logging.properties",
						"yona.adminservice.url"        : project.ext.yona_adminservice_url,
						"yona.analysisservice.url"     : project.ext.yona_analysisservice_url,
						"yona.appservice.url"          : project.ext.yona_appservice_url,
						"yona.batchservice.url"        : project.ext.yona_batchservice_url]
}

bootJar {
	classifier = "full"
	baseName = "analysisservice"
}

eclipse {
	classpath {
		plusConfigurations += [configurations.intTestImplementation]
	}
}

